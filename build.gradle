/*
 * Copyright (c) 2020 CodeHead Systems. All rights reserved
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.


// Testing in Intelij requires the library in your lib path. Errors will show where it needs to be. Example
// from an Ubuntu box
//  sudo mkdir /usr/lib/jni
//  sudo cp -i oop-mock-dynamodb/build/libs/libsqlite4java-linux-amd64-1.0.392.so /usr/lib/jni
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://maven.google.com" } // Google's Maven repository
        gradlePluginPortal() // was jcenter() which is dying
        google()
    }
    dependencies {
        classpath 'org.gretty:gretty:3.0.2'
    }
}

allprojects {
    group 'com.codeheadsystems'
    version '1.0.1-SNAPSHOT'

    apply plugin: "java-library"
    apply plugin: "idea"
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        withJavadocJar()
        withSourcesJar()
    }
    test {
        useJUnitPlatform() // needed for junit5
        finalizedBy jacocoTestReport
    }
    checkstyleTest { // ignore checkstyle in tests
        ignoreFailures true
        showViolations false
    }
    jacocoTestReport {
        dependsOn test
        reports {
            xml.enabled true
            csv.enabled true
        }
        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        '**/dagger/**.class',
                        '**/Dagger*Builder.class',
                        '**/*Exception.class',
                        '**/*Module.class',
                        '**/*_Factory.class',
                        '**/*_Factory.*class',
                        '**/*InstanceHolder.class',
                        '**/*_Factory$InstanceHolder.class'
                ])
            }))
        }
    }
    def excludeFiles = [
            '**.dagger.**',
            '**.Dagger*Builder',
            '**.*Exception',
            '**.*Module',
            '**.*_Factory',
            '**.*_Factory*',
            '**.InstanceHolder',
            '**.*_Factory$InstanceHolder'
    ]

// If you want to override, use this in your package and set. This is the low bar.
    jacocoTestCoverageVerification {
        dependsOn jacocoTestReport
        violationRules {
            rule {
                excludes = excludeFiles
                element = 'CLASS'
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.8
                }
            }
            rule {
                excludes = excludeFiles
                element = 'CLASS'
                limit {
                    counter = 'BRANCH'
                    value = 'COVEREDRATIO'
                    minimum = 0.7
                }
            }
        }
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }
}


ext {
    codeheadTestVersion = '1.0.4'
    codeheadDatabaseTestVersion = '1.0.1'

    awsLocalDDB = "1.15.0" // https://mvnrepository.com/artifact/com.amazonaws/DynamoDBLocal
    awsSDK = "1.12.331" // https://mvnrepository.com/artifact/com.amazonaws/aws-java-sdk-dynamodb
    awsSDK2 = "2.18.6" // https://mvnrepository.com/artifact/software.amazon.awssdk/dynamodb
    aiVersion = '1.8.2' // https://mvnrepository.com/artifact/com.badlogicgames.gdx/gdx-ai
    ashleyVersion = '1.7.4' // https://mvnrepository.com/artifact/com.badlogicgames.ashley/ashley
    assertjVersion = '3.23.1' // https://mvnrepository.com/artifact/org.assertj/assertj-core
    bouncyVersion = '1.70' // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk15on
    box2DLightsVersion = '1.5' // https://mvnrepository.com/artifact/com.badlogicgames.box2dlights/box2dlights
    cassandraDriverVersion = '4.14.1' //  https://mvnrepository.com/artifact/com.datastax.oss/java-driver-core
    cassandraUnitVersion = '4.3.1.0' // https://mvnrepository.com/artifact/org.cassandraunit/cassandra-unit
    commonsIoVersion = '2.11.0' // https://mvnrepository.com/artifact/commons-io/commons-io
    commonsLangVersion = '3.12.0' // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    daggerVersion = '2.44' // https://mvnrepository.com/artifact/com.google.dagger/dagger
    dropwizardVersion = '2.1.4' // https://mvnrepository.com/artifact/io.dropwizard/dropwizard-core
    gdxVersion = '1.11.0' // https://mvnrepository.com/artifact/com.badlogicgames.gdx/gdx
    guavaVersion = '31.1-jre' // https://mvnrepository.com/artifact/com.google.guava/guava
    hsqldbVersion = '2.7.1' // https://mvnrepository.com/artifact/org.hsqldb/hsqldb
    immutablesVersion = '2.9.2' // https://mvnrepository.com/artifact/org.immutables/value
    jacksonVersion = '2.13.4' // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
    jacksonDatabindVersion = '2.13.4.2' // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    jakartaServletVersion = '6.0.0' // https://mvnrepository.com/artifact/jakarta.servlet/jakarta.servlet-api
    javaxInjectVersion = '1'
}

